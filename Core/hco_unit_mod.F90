!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !!
!------------------------------------------------------------------------------
!BOP
!
! !MODULE: hco_unit_mod.F90
!
! !DESCRIPTION: Module HCO\_Unit\_Mod contains routines to check/convert
! units. 
!\\
!\\
! !INTERFACE:
!
MODULE HCO_Unit_Mod
!
! !USES:
!
  USE HCO_Error_Mod    

  IMPLICIT NONE
  PRIVATE
!
! !PUBLIC MEMBER FUNCTIONS:
!
  PUBLIC :: HCO_Unit_Change
  PUBLIC :: HCO_Unit_GetMassScal
  PUBLIC :: HCO_Unit_GetAreaScal
  PUBLIC :: HCO_Unit_GetTimeScal
  PUBLIC :: HCO_Unit_ScalCheck 
  PUBLIC :: HCO_IsUnitLess
  PUBLIC :: HCO_IsIndexData
  PUBLIC :: HCO_UnitTolerance
!
! !PRIVATE MEMBER FUNCTIONS:
!
  PRIVATE :: HCO_Unit_Change_SP
  PRIVATE :: HCO_Unit_Change_DP
!
! !REVISION HISTORY:
!  15 May 2012 - C. Keller   - Initialization
!  08 Jul 2014 - R. Yantosca - Cosmetic changes in ProTeX headers
!  08 Jul 2014 - R. Yantosca - Now use F90 free-format indentation
!  24 Jul 2014 - C. Keller   - Now define unitless & 'standard' units as
!                              parameter
!  13 Aug 2014 - C. Keller   - Interface for sp & dp arrays
!EOP
!------------------------------------------------------------------------------
!BOC
!
! !DEFINED PARAMETERS:
!
  REAL(dp),  PARAMETER :: N_0             = 6.022e+23_dp
  REAL(hp),  PARAMETER :: SEC_IN_DAY      = 86400_hp
  REAL(hp),  PARAMETER :: SEC_IN_LEAPYEAR = SEC_IN_DAY * 366_hp 
  REAL(hp),  PARAMETER :: SEC_IN_REGYEAR  = SEC_IN_DAY * 365_hp
 
  ! Accepted units for unitless data. No unit conversion is applied to 
  ! data with any of these units. 
  ! All characters in this list should be lower case!
  ! The first entry represents the character that denotes unitless data
  ! in the HEMCO configuration file. The second entry represents the 
  ! character that denotes index data.
  INTEGER,           PARAMETER :: NUL = 8
  CHARACTER(LEN=15), PARAMETER :: UL(NUL) = (/ '1',        &
                                               'count',    &
                                               'unitless', &
                                               'fraction', &
                                               'factor',   &
                                               'scale',    &
                                               'hours',    &
                                               'm2/m2'      /)

  ! Accepted units for data on HEMCO standard units. No unit conversion 
  ! is applied to data with any of these units.
  ! All characters in this list should be lower case!
  INTEGER,           PARAMETER :: NHC = 4
  CHARACTER(LEN=15), PARAMETER :: HC(NHC) = (/ 'kg/m2/s',    &
                                               'kgc/m2/s',   &
                                               'kg(c)/m2/s', &
                                               'kg/m3'        /)

  ! Interfaces:
  INTERFACE HCO_UNIT_CHANGE
     MODULE PROCEDURE HCO_UNIT_CHANGE_SP
     MODULE PROCEDURE HCO_UNIT_CHANGE_DP
  END INTERFACE HCO_UNIT_CHANGE

CONTAINS
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_Unit_Change_sp
!
! !DESCRIPTION: Subroutine HCO\_UNIT\_CHANGE\_SP is a wrapper routine to 
! convert the values of the passed single precision array to units of 
! (emitted) kg/m2/s. 
!\\
!\\
! !INTERFACE:
!
  SUBROUTINE HCO_Unit_Change_SP( ARRAY,       UNITS, MW_IN, MW_OUT, &
                                 MOLEC_RATIO, YYYY,  MM,    IsPerArea, RC )
!
! !INPUT PARAMETERS:
!
    CHARACTER(LEN=*), INTENT(IN )     :: UNITS          ! Data unit
    REAL(hp),         INTENT(IN )     :: MW_IN          ! MW g/mol 
    REAL(hp),         INTENT(IN )     :: MW_OUT         ! MW g/mol
    REAL(hp),         INTENT(IN )     :: MOLEC_RATIO    ! molec. ratio
    INTEGER,          INTENT(IN )     :: YYYY           ! Data year 
    INTEGER,          INTENT(IN )     :: MM             ! Data month
!
! !OUTPUT PARAMETERS:
!
    LOGICAL,          INTENT(INOUT)   :: IsPerArea      ! Is per area? 
!
! !INPUT/OUTPUT PARAMETERS:
!
    REAL(sp),         POINTER         :: ARRAY(:,:,:,:) ! Data
    INTEGER,          INTENT(INOUT)   :: RC
!
! !REVISION HISTORY:
!  13 Aug 2014 - C. Keller - Initial Version
!EOP
!------------------------------------------------------------------------------
!BOC
!
! LOCAL VARIABLES:
!
    REAL(hp)     :: Factor

    !=================================================================
    ! HCO_UNIT_CHANGE_SP begins here
    !=================================================================

    CALL HCO_Unit_Factor( UNITS, MW_IN, MW_OUT,    MOLEC_RATIO, &
                          YYYY,  MM,    IsPerArea, Factor,     RC )
    IF ( RC /= HCO_SUCCESS ) RETURN

    ! Apply correction factor
    ARRAY(:,:,:,:) = ARRAY(:,:,:,:) * Factor

    ! Leave
    RC = HCO_SUCCESS

  END SUBROUTINE HCO_Unit_Change_SP
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_Unit_Change_dp
!
! !DESCRIPTION: Subroutine HCO\_UNIT\_CHANGE\_DP is a wrapper routine to 
! convert the values of the passed double precision array to units of 
! (emitted) kg/m2/s. 
!\\
!\\
! !INTERFACE:
!
  SUBROUTINE HCO_Unit_Change_DP( ARRAY,       UNITS, MW_IN, MW_OUT, &
                                 MOLEC_RATIO, YYYY,  MM,    IsPerArea, RC )
!
! !INPUT PARAMETERS:
!
    CHARACTER(LEN=*), INTENT(IN )     :: UNITS          ! Data unit
    REAL(hp),         INTENT(IN )     :: MW_IN          ! MW g/mol 
    REAL(hp),         INTENT(IN )     :: MW_OUT         ! MW g/mol
    REAL(hp),         INTENT(IN )     :: MOLEC_RATIO    ! molec. ratio
    INTEGER,          INTENT(IN )     :: YYYY           ! Data year 
    INTEGER,          INTENT(IN )     :: MM             ! Data month
!
! !OUTPUT PARAMETERS:
!
    LOGICAL,          INTENT(INOUT)   :: IsPerArea      ! Is per area? 
!
! !INPUT/OUTPUT PARAMETERS:
!
    REAL(dp),         POINTER         :: ARRAY(:,:,:,:) ! Data
    INTEGER,          INTENT(INOUT)   :: RC
!
! !REVISION HISTORY:
!  13 Aug 2014 - C. Keller - Initial Version
!EOP
!------------------------------------------------------------------------------
!BOC
!
! LOCAL VARIABLES:
!
    REAL(hp)     :: Factor

    !=================================================================
    ! HCO_UNIT_CHANGE_DP begins here
    !=================================================================

    CALL HCO_Unit_Factor( UNITS, MW_IN, MW_OUT,    MOLEC_RATIO, &
                          YYYY,  MM,    IsPerArea, Factor,     RC )
    IF ( RC /= HCO_SUCCESS ) RETURN

    ! Apply correction factor
    ARRAY(:,:,:,:) = ARRAY(:,:,:,:) * Factor

    ! Leave
    RC = HCO_SUCCESS

  END SUBROUTINE HCO_Unit_Change_DP
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_Unit_Factor
!
! !DESCRIPTION: Subroutine HCO\_UNIT\_Factor calculates the conversion
! factor needed to conver unit UNIT to HEMCO units. 
!\\
!\\
! The mass is in units of kg and refers to mass of emitted species. For
! most compounds, this corresponds to the molecular mass of the species,
! but e.g. for VOCs this can be mass of carbon instead.
! The mass and area/volume conversion is always performed, but the time 
! conversion is only done if a valid time string is provided. For example, 
! if the input unit is kg/cm3 it will be converted to kg/m3, while 
! ug/m2/year is converted to kg/m2/s. If no (valid) area/volume is given in 
! the unit string, the return flag PerArea is set to False (True 
! otherwise).
!\\
!\\ 
! The input argument UNITS refers to the unit of the input data.
! Argument MW\_IN denotes the molecular weight of the input unit
! (g/mol), while MW\_OUT is the molecular weight of the output unit.
! They can differ e.g. for VOCs whose output units are in mass carbon
! instead of mass species. The argument MOLEC\_RATIO is the coefficient 
! used for the conversion of molecules of species to molecules of carbon. 
! For example, MOLEC\_RATIO should be set to 2 for C2H6, which will 
! properly convert kg species (or molec species) to kg C. If the input 
! unit is already on a per carbon basis (e.g. kgC, or molecC), no species
! coefficients will be applied!
!\\
!\\
! Supported unit values:
!\\
!\\
! MASSES:
! \begin{itemize}
! \item molec (includes molecC; molec(C); molec tracer; molecN, molec(N))
! \item atom or atoms (incl. atomC, etc.)
! \item kg, g, mg, ug, ng (incl. kgC, etc.)
! \end{itemize}
!
! TIMES:
! \begin{itemize}
! \item s, sec, hr, hour, d, day, mt, month, y, year.
! \item Valid formats: \/s, s-1, s\^-1. 
! \end{itemize}
!
! VOLUMES/AREAS: 
! \begin{itemize}
! \item cm2, m2, km2, cm3, dm3, m3, l.
! \item Valid formats: \/cm3, cm-3, \/cm\^3, cm\^-3.
! \end{itemize}
!
! The following units will be ignored (no unit conversion is applied):
! \begin{itemize}
! \item unitless
! \item fraction
! \item factor
! \item hours
! \item degC
! \item 1
! \end{itemize}
!
! !INTERFACE:
!
  SUBROUTINE HCO_Unit_Factor( UNITS, MW_IN, MW_OUT,    MOLEC_RATIO, &
                              YYYY,  MM,    IsPerArea, Factor,      RC )
!
! !USES:
!
    USE CharPak_Mod, ONLY : CStrip
!
! !INPUT PARAMETERS:
!
    CHARACTER(LEN=*), INTENT(IN )     :: UNITS          ! Data unit
    REAL(hp),         INTENT(IN )     :: MW_IN          ! MW g/mol 
    REAL(hp),         INTENT(IN )     :: MW_OUT         ! MW g/mol
    REAL(hp),         INTENT(IN )     :: MOLEC_RATIO    ! molec. ratio
    INTEGER,          INTENT(IN )     :: YYYY           ! Data year 
    INTEGER,          INTENT(IN )     :: MM             ! Data month
!
! !OUTPUT PARAMETERS:
!
    LOGICAL,          INTENT(  OUT)   :: IsPerArea      ! Is per area? 
    REAL(hp),         INTENT(  OUT)   :: Factor         ! Conversion factor
!
! !INPUT/OUTPUT PARAMETERS:
!
    INTEGER,          INTENT(INOUT)   :: RC
!
! !REVISION HISTORY:
!  23 Oct 2012 - C. Keller - Initial Version
!  23 May 2013 - C. Keller - Now use additive method
!  01 Oct 2013 - C. Keller - Now convert to kg/m2/s instead of
!                            molec/cm2/s
!  13 Aug 2014 - C. Keller - Split off from subroutine HCO\_UNIT\_CHANGE 
!EOP
!------------------------------------------------------------------------------
!BOC
!
! LOCAL VARIABLES:
!
    REAL(hp)                      :: Coef1
    CHARACTER(LEN=31 )            :: unt
    CHARACTER(LEN=255)            :: MSG
    CHARACTER(LEN=255), PARAMETER :: LOC = 'HCO_UNIT_FACTOR (HCO_UNIT_MOD.F90)'

    !=================================================================
    ! HCO_UNIT_FACTOR begins here
    !=================================================================

    ! Init
    RC        = 0  
    Factor    = 1.0_hp
    Coef1     = 1.0_hp
    IsPerArea = .TRUE.

    ! Get input data unit and strip all blanks. 
    unt = TRIM(UNITS)
    CALL CSTRIP( unt )

    !=================================================================
    ! For special case that data is unitless, a fraction or any other
    ! quantity that shall not be converted - or if it's already in 
    ! units of kg/m2/s.
    !=================================================================
    IF ( HCO_UNIT_SCALCHECK(unt) < 2 ) RETURN

    !=================================================================
    ! Get scale factor for mass. Force to be a valid factor.
    !=================================================================
    Coef1 = HCO_UNIT_GetMassScal ( unt, MW_IN, MW_OUT, MOLEC_RATIO )
    IF ( Coef1 < 0.0_hp ) THEN
       MSG = 'cannot do unit conversion. Mass unit: ' // TRIM(unt)
       CALL HCO_ERROR ( MSG, RC, ThisLoc = LOC )
       RETURN 
    ENDIF
    Factor = Factor * Coef1

    !=================================================================
    ! Get scale factor for time. Skip if invalid factor. This makes
    ! sure that concentrations (e.g. kg/m3) are supported! 
    !=================================================================
    Coef1 = HCO_UNIT_GetTimeScal ( unt, MM, YYYY ) 
    IF ( Coef1 > 0.0_hp ) Factor = Factor * Coef1

    !=================================================================
    ! Get scale factor for area/volume. If no area conversion
    ! factor can be determined, set PerArea flag to False. 
    !=================================================================
    Coef1 = HCO_UNIT_GetAreaScal ( unt ) 
    IF ( Coef1 < 0.0_hp ) THEN
       IsPerArea = .FALSE.
    ELSE
       Factor = Factor * Coef1
    ENDIF

    ! Leave
    RC = HCO_SUCCESS

  END SUBROUTINE HCO_Unit_Factor
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_Unit_GetMassScal
!
! !DESCRIPTION: Returns the mass scale factors for the given unit.
! This is the scale factor required to convert from unit 'Unit' to
! HEMCO units (i.e. kg).
!\\
!\\
! !INTERFACE:
!
  FUNCTION HCO_UNIT_GetMassScal( unt, MW_IN, MW_OUT, MOLEC_RATIO ) &
           RESULT ( Scal ) 
!
! !USES:
!
    USE HCO_CharTools_Mod
!
! !INPUT PARAMETERS:
!
    CHARACTER(LEN=*), INTENT(IN)   :: unt 
    REAL(hp),         INTENT(IN)   :: MW_IN          ! MW g/mol 
    REAL(hp),         INTENT(IN)   :: MW_OUT         ! MW g/mol
    REAL(hp),         INTENT(IN)   :: MOLEC_RATIO    ! molec. ratio
!
! !RETURN VALUE:
!
    REAL(hp)                       :: Scal           ! Scale factor
!
! !REVISION HISTORY:
!  13 Mar 2013 - C. Keller - Initial version
!EOP
!------------------------------------------------------------------------------
!BOC
    CHARACTER(LEN=255)  :: MSG

    !=================================================================
    ! HCO_UNIT_GetMassScal begins here 
    !=================================================================

    ! Init
    Scal = -999.0_hp

    ! Error checks: all passed variables must be defined!
    IF ( MW_IN <= 0.0_hp .OR.  &
         MW_OUT <= 0.0_hp .OR. &
         MOLEC_RATIO <= 0.0_hp  ) THEN
       MSG = 'Cannot determine unit conversion factor for mass - ' // &
             'not all parameter are defined!'
       CALL HCO_MSG(MSG)
       RETURN
    ENDIF

    ! Molecules / atoms of carbon: convert to kg carbon
    ! Molecules / atoms of nitrogen: convert to kg output tracer
    IF ( IsInWord(unt,'molecC'  ) .OR. IsInWord(unt,'molecN')   .OR. &
         IsInWord(unt,'atomC'   ) .OR. IsInWord(unt,'atomsC' )  .OR. &
         IsInWord(unt,'molec(C)') .OR. IsInWord(unt,'atoms(C)') .OR. &
         IsInWord(unt,'atomN'   ) .OR. IsInWord(unt,'atomsN' )  .OR. &
         IsInWord(unt,'molec(N)') .OR. IsInWord(unt,'atoms(N)') .OR. &
         IsInWord(unt,'molectracer') ) THEN
       Scal = MW_OUT * 1e-3_hp / N_0

    ! Molecules / atoms of species: convert to kg output species.
    ELSEIF ( IsInWord(unt,'molec') .OR. IsInWord(unt,'atom') ) THEN
       Scal = MOLEC_RATIO * MW_OUT * 1e-3_hp / N_0

    ! Mols carbon / nitrogen of species
    ELSEIF ( IsInWord(unt,'nmolC') .OR. IsInWord(unt,'nmol(C)') .OR. &
             IsInWord(unt,'nmolN') .OR. IsInWord(unt,'nmol(N)') ) THEN
       Scal = 1e-9_hp * MW_OUT * 1e-3_hp
    ELSEIF ( IsInWord(unt,'umolC') .OR. IsInWord(unt,'umol(C)') .OR. &
             IsInWord(unt,'umolN') .OR. IsInWord(unt,'umol(N)') ) THEN
       Scal = 1e-6_hp * MW_OUT * 1e-3_hp 
    ELSEIF ( IsInWord(unt,'mmolC') .OR. IsInWord(unt,'mmol(C)') .OR. &
             IsInWord(unt,'mmolN') .OR. IsInWord(unt,'mmol(N)') ) THEN
       Scal = 1e-3_hp * MW_OUT * 1e-3_hp 
    ELSEIF ( IsInWord(unt,'molC') .OR. IsInWord(unt,'mol(C)')   .OR. &
             IsInWord(unt,'molN') .OR. IsInWord(unt,'mol(N)') ) THEN
       Scal = MW_OUT * 1e-3_hp 

    ! Mols of species
    ELSEIF ( IsInWord(unt,'nmol') ) THEN
       Scal = 1e-9_hp * MOLEC_RATIO * MW_OUT * 1e-3_hp
    ELSEIF ( IsInWord(unt,'umol') ) THEN
       Scal = 1e-6_hp * MOLEC_RATIO * MW_OUT * 1e-3_hp
    ELSEIF ( IsInWord(unt,'mmol') ) THEN
       Scal = 1e-3_hp * MOLEC_RATIO * MW_OUT * 1e-3_hp
    ELSEIF ( IsInWord(unt,'mol') ) THEN
       Scal = MOLEC_RATIO * MW_OUT * 1e-3_hp

    ! Mass Carbon of species
    ELSEIF ( IsInWord(unt,'ngC') .OR. IsInWord(unt,'ng(C)') ) THEN 
       Scal = 1e-12_hp / 12_hp * MW_OUT 
    ELSEIF ( IsInWord(unt,'ugC') .OR. IsInWord(unt,'ug(C)') ) THEN 
       Scal = 1e-9_hp / 12_hp * MW_OUT 
    ELSEIF ( IsInWord(unt,'mgC') .OR. IsInWord(unt,'mg(C)') ) THEN 
       Scal = 1e-6_hp / 12_hp * MW_OUT 
    ELSEIF ( IsInWord(unt,'kgC') .OR. IsInWord(unt,'kg(C)') ) THEN 
       Scal = 1.0_hp / 12_hp * MW_OUT 
    ELSEIF ( IsInWord(unt,'gC') .OR. IsInWord(unt,'g(C)') ) THEN 
       Scal = 1e-3_hp / 12_hp * MW_OUT

    ! Mass Nitrogen of species
    ELSEIF ( IsInWord(unt,'ngN') .OR. IsInWord(unt,'ng(N)') ) THEN 
       Scal = 1e-12_hp / 14_hp * MW_OUT 
    ELSEIF ( IsInWord(unt,'ugN') .OR. IsInWord(unt,'ug(N)') ) THEN 
       Scal = 1e-9_hp / 14_hp * MW_OUT 
    ELSEIF ( IsInWord(unt,'mgN') .OR. IsInWord(unt,'mg(N)') ) THEN 
       Scal = 1e-6_hp / 14_hp * MW_OUT 
    ELSEIF ( IsInWord(unt,'kgN') .OR. IsInWord(unt,'kg(N)') ) THEN 
       Scal = 1.0_hp / 14_hp * MW_OUT 
    ELSEIF ( IsInWord(unt,'gN') .OR. IsInWord(unt,'g(N)') ) THEN 
       Scal = 1e-3_hp / 14_hp * MW_OUT

    ! Mass of species
    ELSEIF ( IsInWord(unt,'ng') ) THEN
       Scal = 1e-12_hp * MOLEC_RATIO * MW_OUT / MW_IN 
    ELSEIF ( IsInWord(unt,'ug') ) THEN
       Scal = 1e-9_hp * MOLEC_RATIO * MW_OUT / MW_IN 
    ELSEIF ( IsInWord(unt,'mg') ) THEN
       Scal = 1e-6_hp * MOLEC_RATIO * MW_OUT / MW_IN 
    ELSEIF ( IsInWord(unt,'kg') ) THEN
       Scal = MOLEC_RATIO * MW_OUT / MW_IN 
    ELSEIF ( IsInWord(unt,'g') ) THEN
       Scal = 1e-3_hp * MOLEC_RATIO * MW_OUT / MW_IN 
    ENDIF

  END FUNCTION HCO_Unit_GetMassScal
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_Unit_GetTimeScal
!
! !DESCRIPTION: Returns the time scale factors for the given unit.
! This is the scale factor required to convert from unit 'Unit' to
! HEMCO units (i.e. per second).
!\\
!\\
! !INTERFACE:
!
  FUNCTION HCO_Unit_GetTimeScal( unt, MM, YYYY ) RESULT ( Scal ) 
!
! !USES:
!
    USE HCO_CharTools_Mod
!
! !INPUT PARAMETERS:
!
    CHARACTER(LEN=*), INTENT(IN) :: unt   ! This unit
    INTEGER,          INTENT(IN) :: MM    ! Current month
    INTEGER,          INTENT(IN) :: YYYY  ! Current year 
!
! !RETURN VALUE:
!
    REAL(hp)                     :: Scal  ! Scale factor
!
! !REVISION HISTORY:
!  13 Mar 2013 - C. Keller - Initial version
!EOP
!------------------------------------------------------------------------------
!BOC
! 
! !ROUTINE ARGUMENTS:
!
    INTEGER  :: Month, Year
    LOGICAL  :: IS_LEAPYEAR
    INTEGER  :: MONTHDAYS(12) = (/ 31, 28, 31, 30, 31, 30, &
                                   31, 31, 30, 31, 30, 31   /)

    !=================================================================
    ! HCO_UNIT_GetTimeScal begins here 
    !=================================================================

    ! Init
    Scal = -999.0_hp

    ! Is this a leap year?
    Year  = MAX(YYYY,1)
    IS_LEAPYEAR = ( (MOD(Year,4) == 0) .AND. (MOD(Year,400) /= 0) )
    IF ( IS_LEAPYEAR ) MONTHDAYS(2) = 29

    ! second
    IF ( IsInWord(unt,'/s')   .OR. IsInWord(unt,'s-1') .OR. &
         IsInWord(unt,'s^-1') .OR. IsInWord(unt,'sec') ) THEN
       Scal = 1.0_hp

    ! hour
    ELSEIF ( IsInWord(unt,'hr') .OR. IsInWord(unt,'hour') ) THEN
       Scal = 1.0_hp / 3600_hp

    ! day
    ELSEIF ( IsInWord(unt,'/d')   .OR. IsInWord(unt,'d-1') .OR. &
             IsInWord(unt,'d^-1') .OR. IsInWord(unt,'day') ) THEN
       Scal = 1.0_hp / SEC_IN_DAY

      ! month
    ELSEIF ( IsInWord(unt,'mt') .OR. IsInWord(unt,'month') ) THEN
       Month = MAX(MM,1)
       Scal  = 1.0_hp / MONTHDAYS(Month) / SEC_IN_DAY 
      
    ! year
    ELSEIF ( IsInWord(unt,'/y')   .OR. IsInWord(unt,'y-1') .OR. &
             IsInWord(unt,'y^-1') .OR. IsInWord(unt,'yr')  .OR. &
             IsInWord(unt,'year') ) THEN

       IF ( IS_LEAPYEAR ) THEN 
          Scal = 1.0_hp / SEC_IN_LEAPYEAR 
       ELSE
          Scal = 1.0_hp / SEC_IN_REGYEAR 
       ENDIF
    ENDIF
    
  END FUNCTION HCO_UNIT_GetTimeScal
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_Unit_GetAreaScal
!
! !DESCRIPTION: Returns the area/volume scale factors for the given unit.
! This is the scale factor required to convert from unit 'Unit' to
! HEMCO units (i.e. per m2 or per m3).
!\\
!\\
! !INTERFACE:
!
  FUNCTION HCO_Unit_GetAreaScal( unt ) RESULT ( Scal ) 
!
! !USES:
!
    USE HCO_CharTools_Mod
!
! !INPUT PARAMETERS:
!
    CHARACTER(LEN=*), INTENT(IN) :: unt   ! This unit
!
! !OUTPUT PARAMETERS:
!
    REAL(hp)                     :: Scal  ! scale factor
!
! !REVISION HISTORY:
!  13 Mar 2013 - C. Keller - Initial version
!EOP
!------------------------------------------------------------------------------
!BOC
    
    !=================================================================
    ! HCO_UNIT_GetAreaScal begins here 
    !=================================================================

    ! Init
    Scal = -999.0_hp

    ! cm2
    IF ( IsInWord(unt,'/cm2' ) .OR. IsInWord(unt,'cm-2' ) .OR. &
         IsInWord(unt,'/cm^2') .OR. IsInWord(unt,'cm^-2')       ) THEN
       Scal = 1.0_hp / 1e-4_hp

    ! km2
    ELSEIF ( IsInWord(unt,'/km2' ) .OR. IsInWord(unt,'km-2' ) .OR. & 
             IsInWord(unt,'/km^2') .OR. IsInWord(unt,'km^-2')       ) THEN 
       Scal = 1e6_hp

    ! m2
    ELSEIF ( IsInWord(unt,'/m2' ) .OR. IsInWord(unt,'m-2' ) .OR. & 
             IsInWord(unt,'/m^2') .OR. IsInWord(unt,'m^-2')       ) THEN 
       Scal = 1.0_hp

    !=================================================================
    ! Convert volume to m3 
    !=================================================================

    ! cm3
    ELSEIF ( IsInWord(unt,'/cm3')  .OR. IsInWord(unt,'cm-3' ) .OR. &
             IsInWord(unt,'/cm^3') .OR. IsInWord(unt,'cm^-3')       ) THEN 
       Scal = 1e-6_hp

    ! dm3
    ELSEIF ( IsInWord(unt,'/dm3')  .OR. IsInWord(unt,'dm-3' ) .OR. & 
             IsInWord(unt,'/dm^3') .OR. IsInWord(unt,'dm^-3')       ) THEN 
       Scal = 1e-3_hp

    ! m3
    ELSEIF ( IsInWord(unt,'/m3')  .OR. IsInWord(unt,'m-3' ) .OR. & 
             IsInWord(unt,'/m^3') .OR. IsInWord(unt,'m^-3')       ) THEN 
       Scal = 1.0_hp

    ! L
    ELSEIF ( IsInWord(unt,'/l') .OR. IsInWord(unt,'l-1') .OR. & 
             IsInWord(unt,'l^-1')                              ) THEN 
       Scal = 1e-3_hp
    ENDIF

  END FUNCTION HCO_Unit_GetAreaScal
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_Unit_ScalCheck 
!
! !DESCRIPTION: Check if the provided unit is unitless. Returns
! 0 if Unit is unitless, 1 if it's not unitless but in correct
! HEMCO units (i.e. kg/m2/s), 2 otherwise. 
!\\
!\\
! !INTERFACE:
!
  FUNCTION HCO_Unit_ScalCheck( Unit ) Result ( Flag ) 
!
! !USES:
!
    USE CharPak_Mod, ONLY : TRANLC 
!
! !INPUT PARAMETERS:
!
    CHARACTER(LEN=*), INTENT(IN)  :: Unit 
!
! !OUTPUT PARAMETERS:
!
    INTEGER                       :: Flag  ! 0=ok, 1=warning, 2=error
!
! !REVISION HISTORY:
!  13 Mar 2013 - C. Keller - Initial version
!EOP
!------------------------------------------------------------------------------
!BOC
! 
! !LOCAL VARIABLES:
!
    CHARACTER(LEN=31) :: tmpU
    INTEGER           :: I

    !=================================================================
    ! HCO_UNIT_SCALCHECK begins here 
    !=================================================================

    ! Mirror
    tmpU = Unit

    ! lower case
    CALL TRANLC( tmpU )

    ! Error (default):
    Flag = 2

    ! Check for unitless factors
    DO I = 1, NUL
       IF ( TRIM(tmpU) == TRIM(UL(I)) ) THEN
          Flag = 0
          RETURN
       ENDIF
    ENDDO 

    ! Check for HEMCO units
    DO I = 1, NHC
       IF ( TRIM(tmpU) == TRIM(HC(I)) ) THEN
          Flag = 1
          RETURN
       ENDIF
    ENDDO 

  END FUNCTION HCO_Unit_ScalCheck
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_IsUnitless
!
! !DESCRIPTION: Returns TRUE if the passed string corresponds to the HEMCO
! unitless data unit string.
!\\
!\\
! !INTERFACE:
!
  FUNCTION HCO_IsUnitless( Unt ) Result ( Bool )
!
! !INPUT PARAMETERS:
!
    CHARACTER(LEN=*), INTENT(IN)   :: Unt
!
! !OUTPUT PARAMETERS:
!
    LOGICAL                        :: Bool
!
! !REVISION HISTORY:
!  24 Jul 2014 - C. Keller - Initial version
!EOP
!------------------------------------------------------------------------------
!BOC

    Bool = ( TRIM(Unt) == TRIM(UL(1)) )

  END FUNCTION HCO_IsUnitless
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_IsIndexData
!
! !DESCRIPTION: Returns TRUE if the passed string corresponds to the HEMCO
! index data unit string.
!\\
!\\
! !INTERFACE:
!
  FUNCTION HCO_IsIndexData( Unt ) Result ( Bool )
!
! !INPUT PARAMETERS:
!
    CHARACTER(LEN=*), INTENT(IN)   :: Unt
!
! !OUTPUT PARAMETERS:
!
    LOGICAL                        :: Bool
!
! !REVISION HISTORY:
!  24 Jul 2014 - C. Keller - Initial version
!EOP
!------------------------------------------------------------------------------
!BOC

    Bool = ( TRIM(Unt) == TRIM(UL(2)) )

  END FUNCTION HCO_IsIndexData
!EOC
!------------------------------------------------------------------------------
!                  Harvard-NASA Emissions Component (HEMCO)                   !
!------------------------------------------------------------------------------
!BOP
!
! !IROUTINE: HCO_UnitTolerance
!
! !DESCRIPTION: Returns the HEMCO unit tolerance as defined in the HEMCO 
! configuration file (under settings). Returns a default value of 0 (no
! tolerance) if no value is set in the configuration file.
!\\
!\\
! !INTERFACE:
!
  FUNCTION HCO_UnitTolerance( ) Result ( UnitTolerance )
!
! !USES:
!
    USE HCO_EXTLIST_MOD,  ONLY : GetExtOpt
!
! !OUTPUT PARAMETERS:
!
    INTEGER                        :: UnitTolerance
!
! !REVISION HISTORY:
!  24 Jul 2014 - C. Keller - Initial version
!EOP
!------------------------------------------------------------------------------
!BOC
! 
! !LOCAL VARIABLES:
!
    INTEGER, SAVE       :: Tolerance = -1
    LOGICAL             :: FOUND
    INTEGER             :: RC
    CHARACTER(LEN=255)  :: MSG
    
    !=================================================================
    ! HCO_UnitTolerance begins here 
    !=================================================================

    ! On first call, try to get unit tolerance value from core settings.
    ! Use value of zero if not specified in the configuration file.
    IF ( Tolerance < 0 ) THEN
       CALL GetExtOpt ( 0, 'Unit tolerance', OptValInt=Tolerance, &
                        FOUND=FOUND, RC=RC )
       IF ( .NOT. FOUND ) Tolerance = 0

       ! Verbose mode: write to log file
       IF ( HCO_VERBOSE_CHECK() ) THEN
          WRITE(MSG,*) 'Unit tolerance set to ', Tolerance
          CALL HCO_MSG(MSG,SEP1=' ',SEP2=' ')
       ENDIF
    ENDIF

    ! Return
    UnitTolerance = Tolerance

  END FUNCTION HCO_UnitTolerance
!EOC
END MODULE HCO_Unit_Mod
